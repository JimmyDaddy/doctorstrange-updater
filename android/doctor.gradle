import org.apache.tools.ant.taskdefs.condition.Os

def config = project.hasProperty("react") ? project.react : [];

def bundleAssetName =  "doctor.jsbundle"
def entryFile = config.entryFile ?: "index.android.js"

// because elvis operator
def elvisFile(thing) {
    return thing ? file(thing) : null;
}

def reactRoot = elvisFile(config.root) ?: file("../../")
def inputExcludes = config.inputExcludes ?: ["android/**", "ios/**"]

void runBefore(String dependentTaskName, Task task) {
    Task dependentTask = tasks.findByPath(dependentTaskName);
    if (dependentTask != null) {
        dependentTask.dependsOn task
    }
}

gradle.projectsEvaluated {
    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }

    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            // Create variant and target names
            def flavorNameCapitalized = "${productFlavorName.capitalize()}"
            def buildNameCapitalized = "${buildTypeName.capitalize()}"
            def targetName = "${flavorNameCapitalized}${buildNameCapitalized}"
            def targetPath = productFlavorName ?
                    "${productFlavorName}/${buildTypeName}" :
                    "${buildTypeName}"

            // React js bundle directories
            def doctorRoot = "$buildDir/intermediates/assets/${targetPath}/doctor";
            def jsBundleDirConfigName = "jsBundleDir${targetName}"
            def jsBundleDir = elvisFile(config."$jsBundleDirConfigName") ?:
                    file(doctorRoot)

            def resourcesDirConfigName = "resourcesDir${targetName}"
            def resourcesDir = elvisFile(config."${resourcesDirConfigName}") ?:
                    file("${doctorRoot}")
            def jsBundleFile = file("$jsBundleDir/$bundleAssetName")

            // Bundle task name for variant
            def bundleJsAndAssetsTaskName = "doctorbundle${targetName}JsAndAssets"

            // Additional node and packager commandline arguments
            def nodeExecutableAndArgs = config.nodeExecutableAndArgs ?: ["node"]
            def extraPackagerArgs = config.extraPackagerArgs ?: []

            def currentBundleTask = tasks.create(
                    name: bundleJsAndAssetsTaskName,
                    type: Exec) {
                group = "doctor"
                description = "doctor bundle JS and assets for ${targetName}."

                // Create dirs if they are not there (e.g. the "clean" task just ran)
                doFirst {
                    jsBundleDir.mkdirs()
                    resourcesDir.mkdirs()
                }

                // Set up inputs and outputs so gradle can cache the result
                inputs.files fileTree(dir: reactRoot, excludes: inputExcludes)
                outputs.dir jsBundleDir
                outputs.dir resourcesDir

                // Set up the call to the react-native cli
                workingDir reactRoot

                // Set up dev mode
                def devEnabled = !targetName.toLowerCase().contains("release")

                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine("cmd", "/c", *nodeExecutableAndArgs, "node_modules/react-native/local-cli/cli.js", "bundle", "--platform", "android", "--dev", "${devEnabled}",
                            "--reset-cache", "--entry-file", entryFile, "--bundle-output", jsBundleFile, "--assets-dest", resourcesDir, *extraPackagerArgs)
                } else {
                    commandLine(*nodeExecutableAndArgs, "node_modules/react-native/local-cli/cli.js", "bundle", "--platform", "android", "--dev", "${devEnabled}",
                            "--reset-cache", "--entry-file", entryFile, "--bundle-output", jsBundleFile, "--assets-dest", resourcesDir, *extraPackagerArgs)
                }

                enabled config."bundleIn${targetName}" ||
                        config."bundleIn${buildTypeName.capitalize()}" ?:
                        targetName.toLowerCase().contains("release")
            }

            def zipJsAndAssetsTaskName = "doctorZip${targetName}JsAndAssets"
            def zipDir = "$buildDir/intermediates/assets/${targetPath}"

            def doctorZipTask  = tasks.create(
                    name: zipJsAndAssetsTaskName,
                    type: Zip
            ){
                group = "doctor";
                description = "zip bundle JS and assets for ${targetName}."
                from jsBundleDir
                from resourcesDir
                archiveName 'doctor.zip'
                destinationDir(file(zipDir))
            }

            def deleteJsAndAssetsTaskName = "doctorDelete${targetName}JsAndAssets"


            def doctorDeleteResourcesTask = tasks.create(
                    name: deleteJsAndAssetsTaskName,
                    type: Delete
            ){
                group = "doctor";
                description = "Delete bundle JS and assets for ${targetName}."
                delete doctorRoot
                followSymlinks = true

            }

            doctorZipTask.dependsOn(currentBundleTask.getName());
            doctorDeleteResourcesTask.dependsOn(doctorZipTask.getName());

            // Hook bundle${productFlavor}${buildType}JsAndAssets into the android build process
            currentBundleTask.dependsOn("merge${targetName}Resources")
            currentBundleTask.dependsOn("merge${targetName}Assets")

            runBefore("bundle${targetName}JsAndAssets", currentBundleTask)
            runBefore(deleteJsAndAssetsTaskName, doctorZipTask)


            runBefore("process${flavorNameCapitalized}Armeabi-v7a${buildNameCapitalized}Resources", doctorDeleteResourcesTask)
            runBefore("process${flavorNameCapitalized}X86${buildNameCapitalized}Resources", doctorDeleteResourcesTask)
            runBefore("processUniversal${targetName}Resources", doctorDeleteResourcesTask)
            runBefore("process${targetName}Resources", doctorDeleteResourcesTask)
            runBefore("bundle${targetName}JsAndAssets", doctorDeleteResourcesTask)


        }
    }
}
